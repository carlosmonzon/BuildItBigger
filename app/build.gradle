apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"

    defaultConfig {
        applicationId "com.udacity.gradle.builditbigger"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
        free {
            applicationId "com.udacity.gradle.builditbigger.free"
        }
        paid {
            applicationId "com.udacity.gradle.builditbigger.paid"
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestCompile ('com.android.support.test.espresso:espresso-intents:2.2.2'){
        exclude module: 'support-annotations'
    }
    compile project(':joketellerlib')
    compile project(':jokeviewermodule')
    compile 'com.android.support:appcompat-v7:25.3.1'
    freeCompile 'com.google.android.gms:play-services-ads:11.0.4'
    compile 'com.google.code.findbugs:jsr305:2.0.1'
    compile project(path: ':joketellerbackend', configuration: 'android-endpoints')
    compile group: 'com.google.api-client', name: 'google-api-client', version: '1.22.0'
    compile group: 'com.google.api-client', name: 'google-api-client-android', version: '1.22.0'
}

//custom test task
task runBackend(dependsOn: ":joketellerbackend:appengineRun") {
    evaluationDependsOn ":joketellerbackend"
    project(":joketellerbackend") {
        appengine {
            daemon true
        }
    }
}
task stopBackend(dependsOn: ":joketellerbackend:appengineStop")
task customAndroidTest(dependsOn: ":app:connectedAndroidTest")
task testWithBackend(dependsOn: ["runBackend", "customAndroidTest", "stopBackend"])
customAndroidTest.mustRunAfter runBackend
customAndroidTest.finalizedBy stopBackend